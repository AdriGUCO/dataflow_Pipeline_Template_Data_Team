/* https://cloud.google.com/dataform/docs/tables */
/* https://cloud.google.com/dataform/docs/incremental-tables */
/* https://cloud.google.com/dataform/docs/document-tables */
/* https://cloud.google.com/dataform/docs/partitions-clusters */

config {
  type: "incremental",
  uniqueKey: ["TS"],
  description: "average ride length and amount hourly",
  schema: constants.DS_STAGING,
  name: "inc_historical_ride_lenght_and_amount",
  tags: ["bikeshare", "transformations", "incremental"],
  // disabled: true, // Prevents Dataform from creating/compiling this table in BigQuery while keeps the table in the dependency graph.
  // dependencies: ["unreferenced_table"] // dependencies are tables that are not referenced in a ref function in the SELECT statement but needed to be executed before. 
  bigquery: {
    labels: {
      department: "data_engineering",
      query_author: "adria_guerrero", 
    },
    partitionBy: "DATE(TS)",
    // requirePartitionFilter : true,
    updatePartitionFilter: `TS >= ${constants.PARTITION_FILTER_FLUJO}` // set to only consider a subset of records and avoid Dataform scanning the whole table to find matching rows.
    // partitionExpirationDays: 14,
    // clusterBy: ["name", "revenue"]
  },
  columns: {
    INPUT_TS: "procesing time",
    timestamp: {
      description: "date",
      // bigqueryPolicyTags: ["projects/example-project/locations/us/taxonomies/123456789/policyTags/123456789"]
    },
    avg_duration_min: "average trip duration for this date and origin hour",
    trip_count: "number of trips per origin hour",
    /*
    record_name: {
      description: "Description of the record.",
      columns: {
       record_column1_name: "Description of the first record column",
       record_column2_name: "Description of the second record column",
      }
    } 
  */  
  }, 
  assertions: {
    uniqueKey: ["TS"],
    nonNull: ["TS", "INPUT_TS"],
    rowConditions: [ 
      'TS > "1950-01-01"',
      'avg_duration_min > 0',
      'trip_count > 0'
    ]
  }
}

pre_operations {  -- IF NEEDED: SQL operations to execute before the query 
                  -- https://cloud.google.com/dataform/docs/configure-execution#execute-sql-before-table
}

post_operations { -- IF NEEDED: SQL operations to execute after the query
                  -- https://cloud.google.com/dataform/docs/configure-execution#execute-sql-after-table
}

/* QUERY */
SELECT
  CURRENT_TIMESTAMP() AS INPUT_TS,
  TIMESTAMP_TRUNC (start_date, HOUR) AS TS,
  ROUND(AVG(duration_sec/60), 2) AS avg_duration_min,
  COUNT(*) AS trip_count
FROM
  ${ref(`stg_bikeshare_trips`)}
${when(incremental(), `WHERE TIMESTAMP_TRUNC (start_date, HOUR) >= (SELECT TIMESTAMP_SUB(MAX(TS), interval 2 HOUR) FROM ${self()})`)}
-- ${when(incremental(), `WHERE TIMESTAMP_TRUNC (start_date, HOUR) > ${constants.PARTITION_FILTER_DAILY}`)}
GROUP BY 
  TS